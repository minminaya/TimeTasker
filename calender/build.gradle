if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion versions.complile_sdk
    defaultConfig {
//        applicationId "com.minminaya.calender"
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        versionCode versions.calender_version_code
        versionName versions.calender_version_name

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                //组件模式
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                //集成模式
                manifest.srcFile 'src/main//AndroidManifest.xml'
                //去除debug文件夹的文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':base')

    annotationProcessor dep.arouter.arouter_compiler
    annotationProcessor dep.butterknife.butterknife_compiler
}
